#!/usr/bin/env perl

use 5.008;

use strict;
use warnings;

use File::Spec;
use Storable qw{ nstore };

our $VERSION = '0.000_905';

use constant CPAN_INDEX	=> 't/data/_cpan/Metadata';

if ( $ARGV[0] && $ARGV[0] =~ m/ \A --? help \z /smx ) {
    print STDERR <<'EOD';

Generate the module indices used by the various mock objects

Usage:

 $ make-test-data
 $ make-test-data -help

This program requires Storable 2.04, becaue that is what ships with Perl
5.8.0.

EOD
    exit 1;
}

Storable->VERSION le '2.04'
    or die 'Storable 2.04 or less required; you have ',
	Storable->VERSION, "\n";

my %index = (
    'Invalid module'		=> 1,
    'Moose'			=> 1,
    'Test::Pod::LinkCheck'	=> 1,
    'Test::Pod::Links'		=> 1,
    'Test::Pod::No404s'		=> 1,
);

chdir 't/data'
    or die "Unable to cd into t/data: $!";

foreach my $info (
    [ qw{ _cpan Metadata CPAN::Module } ],
) {
    my ( $dir, $fn, $key ) = @{ $info };
    -d $dir
	or mkdir $dir
	or die "Unable to mkdir $dir: $!";
    my $path = File::Spec->catfile( $dir, $fn );
    nstore( { $key => \%index }, $path );
}

{   # Single-iteration loop
    # URLs get handled differently
    my ( $dir, $fn ) = qw{ _http status };
    -d $dir
	or mkdir $dir
	or die "Unable to mkdir $dir: $!";
    my $path = File::Spec->catfile( $dir, $fn );
    my %status = map { $_ => 200 } qw{
	https://metacpan.org/release/Test-Pod-LinkCheck
	https://cpanmetadb.plackperl.org/
	http://rt.cpan.org
    },
    map { "https://cpanmetadb.plackperl.org/v1.0/package/$_" }
    keys %index;
    $status{ 'https://hosed.server/' } = 500;	# Internal server error.
    nstore( \%status, $path );
}


__END__

=head1 TITLE

make-test-data - Generate the module indices used by the various mock objects

=head1 SYNOPSIS

 tools/make-test-data
 tools/make-test-data -help

=head1 OPTIONS


=head2 -help

This option displays the documentation for this script. The script then
exits.

=head1 DETAILS

This Perl script generates the module index files used by the various
mock objects. It is intended to be run from the top level of the
distribution, i.e. as F<tools/make-test-data>.

The files produced are:

=over

=item t/data/_cpan/Metadata

This is a L<CPAN|CPAN>-format metadata file containing B<only> the
module index entries necessary to run F<t/pod_file_ok.t>.

=item t/data/_http/status

This is a L<Storable|Storable> C<nstore()>-format file specifying the
desired HTTP status for any web queries generated by F<t/pod_file_ok.t>.

=back

In order to get these files used, the following actions have to take
place:

=over

=item Set C<$Test::Pod::LinkCheck::Lite::DIRECTORY_LEADER to C<'_'>.

This forces VMS-style directory names for the L<CPAN|CPAN> logic.

=item Set C<$ENV{HOME}> to C<'t/data'>.

This forces the L<CPAN|CPAN> logic to look there for the C<CPAN> index.

=item C<use lib 'inc/Mock'>

This needs to be done before loading
L<Test::Pod::LinkCheck::Lite|Test::Pod::LinkCheck::Lite>, and is
necessary so that the mock version of L<HTTP::Tiny|HTTP::Tiny> will be
loaded.

=back

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2019 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
