#!/usr/bin/env perl

use 5.008;

use strict;
use warnings;

use Getopt::Long 2.33 qw{ :config auto_version };
use Pod::Usage;
use Test::More 0.88;
use Test::Pod::LinkCheck::Lite;

our $VERSION = '0.000_001';

my %opt;

GetOptions( \%opt,
    qw{ man! ua=s },
    help => sub { pod2usage( { -verbose => 2 } ) },
) and 1 >= @ARGV or pod2usage( { -verbose => 0 } );

exists $opt{ua}
    and 'undef' eq $opt{ua}
    and $opt{ua} = undef;

my $tp = Test::Pod::LinkCheck::Lite->new( %opt );

if ( ! @ARGV || -d $ARGV[0] ) {
    $tp->all_pod_files_ok( @ARGV );
} elsif ( -f _ ) {
    $tp->pod_file_ok( $ARGV[0] );
} else {
    $tp->pod_file_ok( \"=pod\n\n$ARGV[0]" );
}

done_testing;

__END__

=head1 TITLE

test-pod - Test a POD file, or literal POD.

=head1 SYNOPSIS

 test-pod fubar.pm
 test-pod 'L<Foobar|Foo/bar>'
 test-pod -help
 test-pod -version

=head1 OPTIONS

=head2 -help

This option displays the documentation for this script. The script then
exits.

=head2 -man

If this Boolean option is asserted, C<man> links will be tested if the
OS is capable of supporting this. If it is negated (as C<-noman>), such
links will not be tested.

The default is true if the host system has a C<man> command, and false
otherwise.

=head2 -ua

 -ua LWP::UserAgent

This option specifies the user agent to use on C<url> links. The default
is the first of L<HTTP::Tiny|HTTP::Tiny> or
L<LWP::UserAgent|LWP::UserAgent> that can be loaded.

=head2 -version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script tests the POD specified as its first and only command
line argument. This can be either the name of a file, or literal POD.

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2019 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
